"""
    Copyright 2017 Inmanta

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

        http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    Contact: code@inmanta.com
"""
import ip
import ip::services

typedef hoststring as string matching /^[A-Za-z0-9-]+(\.[A-Za-z0-9-]+)*\.?$/

entity Zone:
    """
        A dns zone.
    """
    string hostmaster
    string domain
    number refresh=7200
    number retry=600
    number expiry=1209600
    number ttl=3600
    bool add_ns=false
end

entity SlaveZone extends Zone:

end

implementation addNS for Zone:
    NS(server="{{master.host.name}}.", zone=self)
    for slave in slaves:
        NS(server="{{slave.host.name}}.", zone=self)
    end
end

implement Zone using std::none
implement SlaveZone using std::none

Zone.master [1] -- DnsServer.master_zones [0:]
Zone.slaves [0:] -- DnsServer.slave_zones [0:]

entity DnsServer:
    ip::ip ipaddress
end

implement DnsServer using std::none

entity Server extends ip::services::Server, DnsServer:
    """
        A dns server
    """
    bool recursive=true
    string allow_recursion=""
    string forwarders=""
    # constraint: a dns record should exist for this hostname!
end

entity ResourceRecord:
    """
        A generic dns resource record
    """
    string resource=""
    string value
    string record_type
end
implement ResourceRecord using std::none

ResourceRecord.zone [1] -- Zone.records [0:]

entity Record:
    """
        A base class for high level record entities
    """
    string resource=""
end

Record.zone [1] -- Zone

entity A extends Record:
    """
        An A record

        :attr ipaddress: The address to point this record to
    """
    ip::ip ipaddress
end
implement A using aImpl

implementation aImpl for A:
    ResourceRecord(zone=self.zone, resource=self.resource, record_type="a", value=self.ipaddress)
end

entity NS extends Record:
    hoststring server
end
implement NS using nsImpl

implementation nsImpl for NS:
    ResourceRecord(zone=self.zone, resource=self.resource, record_type="ns", value=self.server)
end

entity Cname extends Record:
    string value
end
implement Cname using cnameImpl

implementation cnameImpl for Cname:
    self.record_type = "cname"
    ResourceRecord(zone=self.zone, resource=self.resource, record_type="cname", value=self.value)
end

entity MX extends Record:
    hoststring server
    number priority=10
end
implement MX using mxImpl

implementation mxImpl for MX:
    ResourceRecord(zone=self.zone, resource=self.resource, record_type="mx", value="{{ priority }} {{ server }}")
end

entity AAAA extends Record:
    string ipaddress
end
implement AAAA using aaaaImpl

implementation aaaaImpl for AAAA:
    ResourceRecord(zone=self.zone, resource=self.resource, record_type="aaaa", value=self.ipaddress)
end

entity PTR extends Record:
    string name
    ip::ip ipaddress
end

implementation ptrImpl for PTR:
    ResourceRecord(zone=self.zone, resource=dns::ip_to_arpa(ipaddress), record_type="ptr", value=self.name)
end

implement Record using std::none
implement PTR using ptrImpl

entity TXT extends Record:
    string data
end

implementation txtImpl for TXT:
    ResourceRecord(zone=self.zone, resource=self.resource, record_type="txt", value=dns::quote(self.data))
end

implement TXT using txtImpl
